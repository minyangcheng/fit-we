apply plugin: 'com.android.application'
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.fit.we.sample"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        //必须加，否则64位手机无法加载so文件
        ndk {
            abiFilters "x86"
            abiFilters "armeabi"
        }
        buildConfigField 'String', 'GIT_VERSION', "\"${getGitVersion()}\""
        buildConfigField 'String', 'fitWeHostServer', '"https://dist.cheguo.com"'
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }
    }
    productFlavors {
        dev {
            buildConfigField 'String', 'fitWeServer', "\"${getIpAddress()}\""
        }
        beta {
            buildConfigField 'String', 'fitWeServer', '"http://dist.cheguo-cdn.com"'
        }
        preview {}
        product {}
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    testCompile 'junit:junit:4.12'
    compile project(':fit-we')
}

def getIpAddress() {
    InetAddress inetAddress=getLocalHostLANAddress();
    return 'http://'+inetAddress.getHostAddress()+":8888";
}

def getLocalHostLANAddress() {
    try {
        InetAddress candidateAddress = null;
        for (Enumeration ifaces = NetworkInterface.getNetworkInterfaces(); ifaces.hasMoreElements();) {
            NetworkInterface iface = (NetworkInterface) ifaces.nextElement();
            for (Enumeration inetAddrs = iface.getInetAddresses(); inetAddrs.hasMoreElements();) {
                InetAddress inetAddr = (InetAddress) inetAddrs.nextElement();
                if (!inetAddr.isLoopbackAddress()) {
                    if (inetAddr.isSiteLocalAddress()) {
                        return inetAddr;
                    } else if (candidateAddress == null) {
                        candidateAddress = inetAddr;
                    }
                }
            }
        }
        if (candidateAddress != null) {
            return candidateAddress;
        }
    } catch (Exception e) {
        e.printStackTrace();
    }
    return InetAddress.getLocalHost();
}

def getGitVersion() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}
